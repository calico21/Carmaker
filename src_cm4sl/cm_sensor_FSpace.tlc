%%
%%  Copyright Â©1998-2025 IPG Automotive GmbH. All rights reserved.
%%  www.ipg-automotive.com
%%
%implements "cm_sensor_FSpace" "C"

%include "cm_rtwlib.tlc"


%function BlockTypeSetup(block, system) void
    %<LibAddToCommonIncludes("<Global.h>")>
    %if CM_IsRtiTarget || CM_IsDsRtTarget
	%<LibAddToCommonIncludes("<CM_Sfun_Utils.h>")>
    %endif
    %<LibAddToCommonIncludes("<SimCore.h>")>
    %<LibAddToCommonIncludes("<Vehicle/Sensor_FSpace.h>")>
%endfunction


%function Start (block, system) Output
    %if CM_IsCarMakerTarget
	%assign sisfirststep = LibBlockIWork(SIsFirstStep, "", "", 0)

	%<sisfirststep> = 1;
    %else
	%assign _sname = block.SFcnParamSettings[0]._SName
	%assign sname  = block.SFcnParamSettings[0].SName
	%assign shasextindex = CAST("Boolean", block.SFcnParamSettings[0].SHasExtIndex)

	%if !%<shasextindex>
	    %assign shandle = LibBlockIWork(SHandle, "", "", 0)
	    %<shandle> = MdlItems_Add(MdlItem_FSpaceSensor, "%<sname>", MdlIdx_Default); // %<_sname>
	%endif
    %endif
%endfunction


%function Outputs (block, system) Output
    %assign shasextindex    = block.SFcnParamSettings[0].SHasExtIndex != 0
    {
	const tFSpaceSensor *fs;
	int fs_idx;

	%if CM_IsCarMakerTarget
	    static tFSpaceSensor ZeroSensor = { { { 0 } } };

	    %if %<shasextindex>
		fs_idx = %<LibBlockInputSignal(0, "", "", 0)>;
		if (SimCore.State != SCState_Simulate ||
		    (fs = FSpaceSensor_GetByIndex(fs_idx)) == NULL) {
		    fs = &ZeroSensor;
		    fs_idx = -1;
		}
	    %else
		%assign _sname          = block.SFcnParamSettings[0]._SName
		%assign sname           = block.SFcnParamSettings[0].SName
		%assign salphaindex     = CAST("Number", block.SFcnParamSettings[0].SAlphaIndex)
		%assign sindex          = LibBlockIWork(SIndex, "", "", 0)
		%assign sisfirststep    = LibBlockIWork(SIsFirstStep, "", "", 0)
		%assign ssensor         = LibBlockPWork(SSensor, "", "", 0)

		if (%<sisfirststep>) {
		    %if %<salphaindex> >= 0
			%<sindex> = %<salphaindex>;
		    %else
			%<sindex> = FSpaceSensor_FindIndexForName("%<sname>"); // %<_sname>
		    %endif
		    %<ssensor> = FSpaceSensor_GetByIndex(%<sindex>);
		    if (%<ssensor> == NULL) {
			%<ssensor> = &ZeroSensor;
			%<sindex> = -1;
		    }
		    %<sisfirststep> = 0;
		}
		fs_idx = %<sindex>;
		fs = (const tFSpaceSensor *)%<ssensor>;
	    %endif
	%else
	    %if %<shasextindex>
		fs_idx = %<LibBlockInputSignal(0, "", "", 0)>;
		if (SimCore.State != SCState_Simulate ||
		    (fs = FSpaceSensor_GetByIndex(fs_idx)) == NULL) {
		    fs = MdlZero.FSpaceSensor;
		    fs_idx = -1;
		}
	    %else
		%assign shandle = LibBlockIWork(SHandle, "", "", 0)
		const struct tMdlItemRef *ms = &MdlItems.Entries[%<shandle>].s[MdlItemIndex];
		fs = ms->Item;
		fs_idx = ms->Index;
	    %endif
	%endif

	%assign sig0 = LibBlockOutputSignal(0, "", "", 0)
	%assign sig1 = LibBlockOutputSignal(1, "", "", 0)

	// Port 0: FSpaceSensor index
	%<sig0> = fs_idx;

	// Port 1: FSpaceSensor general data
	%<sig1>.tx_ext          = fs->t_ext[0];
	%<sig1>.ty_ext          = fs->t_ext[1];
	%<sig1>.tz_ext          = fs->t_ext[2];
	%<sig1>.rx_ext       	= fs->rot_zyx_ext[0];
	%<sig1>.ry_ext       	= fs->rot_zyx_ext[1];
	%<sig1>.rz_ext       	= fs->rot_zyx_ext[2];
	%<sig1>.TimeStamp       = fs->TimeStamp;
    }
%endfunction
